plugins {
    id "com.android.application" version "8.10.0" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
    id "com.google.gms.google-services" version "4.4.0" apply false
    id "com.google.firebase.crashlytics" version "3.0.2" apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Fix for android:attr/lStar error with isar_flutter_libs and API 35
subprojects { subproject ->
    if (subproject.name == 'isar_flutter_libs') {
        subproject.afterEvaluate {
            if (subproject.hasProperty('android')) {
                subproject.android {
                    compileSdkVersion 36
                    buildToolsVersion "34.0.0"
                }
            }
        }
    }
}

// Fix for Firebase packages that don't have compileSdkVersion
subprojects { subproject ->
    if (subproject.name.contains('firebase') || 
        subproject.name.contains('google') ||
        subproject.name.contains('image_picker') ||
        subproject.name.contains('path_provider') ||
        subproject.name.contains('shared_preferences')) {
        subproject.afterEvaluate {
            if (subproject.hasProperty('android')) {
                subproject.android {
                    if (!subproject.android.hasProperty('compileSdkVersion')) {
                        compileSdkVersion 36
                    }
                    if (!subproject.android.hasProperty('buildToolsVersion')) {
                        buildToolsVersion "34.0.0"
                    }
                }
            }
        }
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
